buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"

        }
        maven { 
            url "https://repo.grails.org/grails/core" 
        }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate4:5.0.10"
        classpath "org.grails.plugins:views-gradle:1.0.12"
        classpath "com.moowork.gradle:gradle-node-plugin:0.11" // version 13 does not work
    }
}

version "0.1"
group "chat"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.plugins.views-json"
apply plugin: "com.moowork.node"
ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

node {
    version = '6.4.0'
    download = true
    nodeModulesDir = file("${project.projectDir}/frontend")
}

task cleanPublic(type: Delete) {
    delete './src/main/resources/public'
}

task buildFrontend(type: NpmTask, dependsOn: [cleanPublic, npmInstall]) {
    workingDir './frontend'
    args = ['run', 'build']

    inputs.files file('./frontend')
    outputs.files file('frontend/build')
}

task copyFrontendToPublic(type: Copy, dependsOn: [buildFrontend]) {
    from "frontend/build"
    into "./src/main/resources/public"
}

processResources.dependsOn copyFrontendToPublic

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-codecs"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-datasource"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-async"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:hibernate4"
    compile "org.hibernate:hibernate-ehcache"
    compile "org.grails.plugins:views-json"
    console "org.grails:grails-console"
    profile "org.grails.profiles:rest-api"
    runtime "com.h2database:h2"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testCompile "org.grails:grails-datastore-rest-client"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
    compile "org.grails.plugins:grails3-cors-interceptor:1.1.0" // YES!
}
